# .zprofile - login shell initialization file

[[ "$(id -gn)" == "$USERNAME" ]] && umask 002 || umask 022

# • path and zsh functions                                               {{{1

export LD_LIBRARY_PATH=""
export PATH="$HOME/bin:$PATH"
for file in $HOME/.{zshenv,zprofile,zshrc,zlogout}; do
  [[ $file -ot $file.zwc ]] || zcompile $file
done

function zshfunctions() {
  [[ -d "$FUNCTIONS" ]] && zcompile -Uz $FUNCTIONS $FUNCTIONS/*(.x) || :
}
zshfunctions

# • system setup                                                         {{{1

export EDITOR="vim"
export TERMINAL="alacritty"
export MAKEFLAGS='-j -Otarget'

export PAPERSIZE="a4"
export PRINTER="kmc368"

export LESS="-cx3MRFX"
export LESSCOLORIZER="pygmentize"
export LESSOPEN="| lesspipe %s"

unset  SSH_AGENT_PID
export SSH_AUTH_SOCK="${XDG_RUNTIME_DIR}/gnupg/S.gpg-agent.ssh"

# • graphic system (wayland/x11)                                         {{{1

export DE="generic"
export SAL_USE_VCLPLUGIN=qt5
export QT_QPA_PLATFORMTHEME="qt5ct"
export LIBVA_DRIVER_NAME="iHD"

export QT_IM_MODULE="fcitx"
export GTK_IM_MODULE="fcitx"
export XMODIFIERS="@im=fcitx"

read _JAVA_OPTIONS <<end
  -Dsun.java2d.opengl=true -Dawt.useSystemAAFontSettings=on \
  -Dswing.aatext=true
end
export _JAVA_OPTIONS
export _JAVA_AWT_WM_NONREPARENTING=1
export JAVA_FONTS="/usr/share/fonts/TTF"

# • Local development configuration                                      {{{1

local="$HOME/usr/local"
for dir in $local/*(-/); do
  [[ -d "$dir/bin"   ]] && PATH="$dir/bin:$PATH"
  [[ -d "$dir/lib"   ]] && LD_LIBRARY_PATH="$dir/lib:$LD_LIBRARY_PATH"
  [[ -d "$dir/lib64" ]] && LD_LIBRARY_PATH="$dir/lib64:$LD_LIBRARY_PATH"
done

# • Location specific configuration                                      {{{1

if [[ "$TTY" == "/dev/tty1" ]]; then
  # first TTY automatically run GUI
  exec startx

elif [[ "$TTY" =~ "/dev/tty[2-9]" ]]; then
  # local TTY: set some pretty colors
  tput setab 7
  tput setaf 0
  echo -ne "\e]P04d4d4c\e]P8000000"
  echo -ne "\e]P1c82829\e]P9ff3334"
  echo -ne "\e]P2718c00\e]PA9ec400"
  echo -ne "\e]P3f5871f\e]PBeab700"
  echo -ne "\e]P44271ae\e]PC5795e6"
  echo -ne "\e]P58959a8\e]PDb777e0"
  echo -ne "\e]P63e999f\e]PE54ced6"
  echo -ne "\e]P7fafafa\e]PF8e908c"
  echo -ne "\e[8]\e[1;15]"
  clear

else
  # remote login shell
  export REMOTE_SESSION=true

  # ignore other system-wide zsh configuration
  unsetopt GLOBAL_RCS

  # set TZ to my time zone on remote servers
  export TZ='CST+6'

  # fix some common version problems
  [[ "${$(make -v)[3]}" -lt 4 ]] && export MAKEFLAGS='-j'

  # overwrite local system
  if [[ -d "$local/vim" ]] then
    export VIM="$local/vim/share/vim"
    export VIMRUNTIME="$VIM/vim90"
  fi
fi
